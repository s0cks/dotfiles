{
  local NEWLINE = [ '' ],
  local Optional(value, cond, default_value = []) =
    if cond then
      value
    else
      default_value,
  local OptionalNewline(cond, default_value = []) =
    Optional(NEWLINE, cond, default_value),
  local WrapInOptionalNewlines(value, newline_before = false, newline_after = false) =
    OptionalNewline(newline_before) +
    value +
    OptionalNewline(newline_after),
  Quote(value):
    '"' + value + '"',
  Array(values, quote = false):
    '(' +
      std.join(' ', [
        (if quote then $.Quote(value) else value)
        for value in (if std.isArray(values) then values else [ values ])
      ]) +
    ')',
  Comment(lines, newline_before = false, newline_after = false):
    WrapInOptionalNewlines(
      [
        "# " + line
        for line in (if std.isArray(lines) then lines else [ lines ])
      ], newline_before, newline_after),
  Export(key, value, newline_before = false, newline_after = false):
    WrapInOptionalNewlines(
      [
        "export " + key + "=" +
          (if std.isString(value) then
            $.Quote(value)
          else if std.isArray(value) then
            $.Array(value)
          else
            value)
      ],
      newline_before, newline_after),
  ExportPath(value, newline_before = false, newline_after = false):
    $.Export("PATH", value, newline_before, newline_after),
  ExportPathPrepend(value, newline_before = false, newline_after = false):
    $.ExportPath(value + ":$PATH", newline_before, newline_after),
  ExportPathAppend(value, newline_before = false, newline_after = false):
    $.ExportPath("$PATH:" + value, newline_before, newline_after),
  ExportFpath(values, quote = false, newline_before = false, newline_after = false):
    WrapInOptionalNewlines(
      [
        "export fpath=" + $.Array(values, quote),
      ],
      newline_before, newline_after),
  ExportFpathPrepend(values, quote = false, newline_before = false, newline_after = false):
    $.ExportFpath(values + [ "${fpath[@]}" ], quote, newline_before, newline_after),
  Shebang(exec = "zsh", path = "/usr/bin/env", newline_after = false):
    $.Comment("#!" + path + " " + exec, false, newline_after),
  Header(lines, shebang = true, newline_after = false):
    Optional($.Shebang(), shebang) +
    $.Comment(lines) +
    OptionalNewline(newline_after),
  Alias(key, value, newline_before = false, newline_after = false):
    WrapInOptionalNewlines(
      [
        "alias " + key + "=" + $.Quote(value),
      ],
      newline_before, newline_after),
  GlobalAlias(key, value, newline_before = false, newline_after = false):
    $.Alias("-g " + key, value, newline_before, newline_after),
  SuffixAlias(key, value, newline_before = false, newline_after = false):
    $.Alias("-s " + key, value, newline_before, newline_after),
  EditorSuffixAlias(key, newline_before = false, newline_after = false):
    $.SuffixAlias(key, "$EDITOR", newline_before, newline_after),
  HashDir(key, value, newline_before = false, newline_after = false):
    WrapInOptionalNewlines(
      [
        "hash -d " + key + "=" + $.Quote(value),
      ],
      newline_before, newline_after),
  manifest(lines):
    std.lines(std.flattenDeepArray(
      $.Header([
        "*** Do not edit ***",
        "This file is auto-generated by Jsonnet",
      ]) +
      lines
    ))
}
