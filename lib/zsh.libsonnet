local util = import 'lib/util.libsonnet';

{
  Array(values, quote = false):
    '(' +
      std.join(' ', [
        (if quote then util.Quote(value) else value)
        for value in (if std.isArray(values) then values else [ values ])
      ]) +
    ')',
  Comment(lines, newline_before = false, newline_after = false):
    util.WrapInOptionalNewlines(
      [
        "# " + line
        for line in (if std.isArray(lines) then lines else [ lines ])
      ], newline_before, newline_after),
  Export(key, value, newline_before = false, newline_after = false):
    util.WrapInOptionalNewlines(
      [
        "export " + key + "=" +
          (if std.isString(value) then
            util.Quote(value)
          else if std.isArray(value) then
            $.Array(value)
          else
            value)
      ],
      newline_before, newline_after),
  Exports(data, newline_before = false, newline_after = false):
    util.WrapInOptionalNewlines(
      [
        $.Export(k, data[k])
        for k in std.objectFields(data)
      ], newline_before, newline_after),
  ExportPath(value, newline_before = false, newline_after = false):
    $.Export("PATH", value, newline_before, newline_after),
  ExportPathPrepend(value, newline_before = false, newline_after = false):
    $.ExportPath(value + ":$PATH", newline_before, newline_after),
  ExportPathAppend(value, newline_before = false, newline_after = false):
    $.ExportPath("$PATH:" + value, newline_before, newline_after),
  ExportFpath(values, quote = false, newline_before = false, newline_after = false):
    util.WrapInOptionalNewlines(
      [
        "export fpath=" + $.Array(values, quote),
      ],
      newline_before, newline_after),
  ExportFpathPrepend(values, quote = false, newline_before = false, newline_after = false):
    $.ExportFpath(values + [ "${fpath[@]}" ], quote, newline_before, newline_after),
  Alias(keys, value, newline_before = false, newline_after = false, prefix = null):
    util.WrapInOptionalNewlines(
      [
        "alias" + (if prefix != null then " " + prefix + " " else " " ) + key + "=" + util.Quote(value),
        for key in (if std.isArray(keys) then keys else [ keys ])
      ],
      newline_before, newline_after),
  GlobalAlias(keys, value, newline_before = false, newline_after = false):
    $.Alias(keys, value, newline_before, newline_after, "-g"),
  GlobalAliasMap(aliases):
    [
      $.GlobalAlias(name, aliases[name])
      for name in std.objectFields(aliases)
    ],
  SuffixAlias(keys, value, newline_before = false, newline_after = false):
    $.Alias(keys, value, newline_before, newline_after, "-s"),
  EditorSuffixAlias(keys, newline_before = false, newline_after = false):
    $.SuffixAlias(keys, "$EDITOR", newline_before, newline_after),
  HashDir(key, value, newline_before = false, newline_after = false):
    util.WrapInOptionalNewlines(
      [
        "hash -d " + key + "=" + util.Quote(value),
      ],
      newline_before, newline_after),
  Local(key, value = null, newline_before = false, newline_after = false):
    util.WrapInOptionalNewlines(
      [
        "local " + key + (if value != null then "=" + util.Quote(value) else "")
      ], newline_before, newline_after),
  LocalMap(data, newline_before = false, newline_after = false):
    util.WrapInOptionalNewlines([
      "local " + name + (if name in data then "=" + util.Quote(data[name]) else "")
      for name in std.objectFields(data)
    ]),
  Hashes(hashes):
    [
      $.HashDir(name, hashes[name])
      for name in std.objectFields(hashes)
    ],
  Source(value):
    [
      "source " + value
    ],
  local CombineConds(conds, op = " && ") =
    std.join(op, conds),
  local CondWrap(cond) =
    "[[ " + cond + " ]]",
  local CommandExists(commands) =
    [ 
      "$+commands[" + command + "]"
      for command in (if std.isArray(commands) then commands else [ commands ])
    ],
  SourceIfCommand(value, commands, op = " && "):
    CondWrap(CombineConds(CommandExists(commands), op)) + " && source " + value,
  BindKey(binding, value):
    [
      "bindkey " + util.SingleQuote(binding) + " " + value,
    ],
  Shebang(exec = "zsh", path = "/usr/bin/env", newline_before = false, newline_after = false):
    util.WrapInOptionalNewlines([
      "#!" + path + " " + exec,
    ], newline_before, newline_after),
  Header(lines, shebang = true, newline_after = false):
    util.Optional($.Shebang(), shebang) +
    $.Comment(lines) +
    util.OptionalNewline(newline_after),
  Autoload(value, flag = "-Uz"):
    "autoload " + flag + " " + value,
  AutoloadDir(dir, flag = "-Uz"):
    $.Autoload(dir + "/*(:t)", flag),
  manifest(lines):
    std.lines(std.flattenDeepArray(
      $.Header([
        "*** Do not edit ***",
        "This file is auto-generated by Jsonnet",
        "~ s0cks",
      ], true, true) + 
      lines
    ))
}
