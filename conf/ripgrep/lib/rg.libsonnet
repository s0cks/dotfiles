
// columns
// --max-columns=60
// --max-columns-preview
// ignore case
// --smart-case
// follow symlinks
// --follow
// include hidden files
// --hidden
// exclude .git directory
// --glob=!.git/*


{
  local NEWLINE = [ '' ],
  local Optionally(value, cond, default_value = []) =
    if cond then
      value
    else
      default_value,
  local OptionalNewline(cond) = Optionally(NEWLINE, cond),
  Comment(lines, newline_before = false, newline_after = false):
    OptionalNewline(newline_before) +
    [
      "# " + line
      for line in (if std.isArray(lines) then lines else [ lines ])
    ] +
    OptionalNewline(newline_after),
  local Flag(name, value = null, newline_before = false, newline_after = false) =
    OptionalNewline(newline_before) +
    [
      if value != null then
        "--%(name)s=%(value)s" % { name: name, value: value }
      else
        "--%(name)s" % { name: name },
    ] +
    OptionalNewline(newline_after),
  local Switch(name, newline_before = false, newline_after = false) = 
    Flag(name, null, newline_before, newline_after),
  MaxColumns(value, newline_before = false, newline_after = false):
    Flag("max-columns", value, newline_before, newline_after),
  MaxColumnsPreview(newline_before = false, newline_after = false):
    Switch("max-columns-preview", newline_before, newline_after),
  SmartCase(newline_before = false, newline_after = false):
    Switch("smart-case", newline_before, newline_after),
  Follow(newline_before = false, newline_after = false):
    Switch("follow", newline_before, newline_after),
  Hidden(newline_before = false, newline_after = false):
    Switch("hidden", newline_before, newline_after),
  Glob(value, newline_before = false, newline_after = false):
    Flag("glob", value, newline_before, newline_after),
  GlobIgnore(value, newline_before = false, newline_after = false):
    $.Glob("!" + value, newline_before = false, newline_after = false),
  GitGlob(newline_before = false, newline_after = false):
    $.GlobIgnore(".git/*", newline_before, newline_after),
  TypeAdd(name, pattern, newline_before = false, newline_after = false):
    OptionalNewline(newline_before) +
    $.Comment(name + " types") +
    [
      "--type-add",
      name + ":" + pattern,
    ] +
    OptionalNewline(newline_after),
  Config(values, newline_before = false, newline_after = false):
    std.lines(std.flattenDeepArray(
      OptionalNewline(newline_before) +
      $.Comment([
        "*** Do not edit ***",
        "This file is auto-generated by Jsonnet",
      ]) +
      values +
      OptionalNewline(newline_after))),
}
