local flexoki = import 'lib/flexoki.libsonnet';
{
  local EMPTY = '',
  local OptionalNewline(cond) =
    (if cond then [ EMPTY ] else []),
  KeyMappings():
      {
        ["key_mappings"]: {
          next_item: ["down","tab", "ctrl+j", "ctrl+n"],
          previous_item: ["up", "shift+tab", "ctrl+k", "ctrl+p"],
          jump_ten_next_items: ["pagedown"],
          jump_ten_previous_items: ["pageup"],
          go_to_first_item: ["ctrl+up", "ctrl+home"],
          go_to_last_item: ["ctrl+down", "ctrl+end"],
          close: ["esc"],
          quit: ["ctrl+c"],
          kill_process: ["ctrl+x"],
          refresh_process_list: ["ctrl+r"],
          copy_process_pid: ["ctrl+y"],
          scroll_process_details_down: ["ctrl+f"],
          scroll_process_details_up: ["ctrl+b"],
          select_process_parent: ["alt+p"],
          select_process_family: ["alt+f"],
          select_process_siblings: ["alt+s"],
          toggle_help: ["ctrl+h"],
          toggle_debug: ["alt+d"],
          cursor_left: ["left"],
          cursor_right: ["right"],
          cursor_home: ["home"],
          cursor_end: ["end"],
          delete_char: ["backspace"],
          delete_next_char: ["delete"],
          delete_word: ["ctrl+w"],
          delete_to_start: ["ctrl+u"],
        },
      },
  Comment(lines, newline_before, newline_after):
    OptionalNewline(newline_before) + 
    [
      "# %(line)s" % { line: line }
      for line in (if std.isArray(lines) then lines else [ lines ])
    ] +
    OptionalNewline(newline_after),
  ProcessDetailsStyle(p):
    {
      title: { alignment: "left", position: "top" },
      border: { type: "rounded", style: {fg: p.tx }},
    },
  ProcessTableStyle(p):
    {
      title: { alignment: "left", position: "top" },
      border: { type: "rounded", style: { fg: p.tx } },
    },
  ProcessTableRowStyle(p):
    {
      selected_symbol: " ",
      even: { fg: p.tx, bg: p.bg },
      odd: { fg: p.tx, bg: p.bg },
      selected: { fg: p.tx, add_modifier: "REVERSED" },
    },
  ProcessTableCellStyle(p):
    {
      highlighted: { bg: p.bg2, add_modifier: "ITALIC" },
    },
  UI(p):
    {
      "ui": {
        icons: "nerd_font_v3",
      },
      "ui.popups": {
        border: {type: "rounded", style: {fg: p.ui}},
        selected_row: { bg: p.bg, add_modifier: "BOLD"},
        primary: { fg: p.tx },
      },
      ["ui.search_bar"]: {
        cursor_style: {add_modifier: "REVERSED"}
      },
      ["ui.process_details.scrollbar"]: {
        track_symbol: "│",
        begin_symbol: "↑",
        end_symbol: "↓",
        margin: {horizontal: 0, vertical: 1},
      },
      "ui.process_details": $.ProcessDetailsStyle(p),
      ["ui.process_table"]: $.ProcessTableStyle(p),
      ["ui.process_table.row"]: $.ProcessTableRowStyle(p),
      ["ui.process_table.cell"]: $.ProcessTableCellStyle(p),
    },
  local Indent = std.repeat(' ', 2),
  Config():
    std.lines($.Comment([
      "*** Do not edit ***",
      "This file is auto-generated by Jsonnet",
    ], false, true)) +
    std.manifestTomlEx({
      screen_size: { height: 50 },
      ["ignore"]: {
        // ignore processes that path matches any of given regex
        paths: [],
        // paths: ["/System/.*", "Applications/.*"]
        // ignore other users processes
        other_users: true,
        // ignore thread processes (on linux)
        threads: true,
      },
    } +
    $.UI(flexoki.Dark) +
    $.KeyMappings(), Indent),
}
