{
  local NEWLINE = [ "" ],
  Comment(text, newline_before = false, newline_after = false):
    (if newline_before then NEWLINE else []) +
    [
      "# %(text)s" % { text: line }
      for line in (if std.isArray(text) then text else [ text ])
    ] +
    (if newline_after then NEWLINE else []),
  Config(name, value):
    "yabai -m config %(name)s %(value)s" % {
      name: name,
      value: value,
    },
  local LabelLastSpace(label) =
    "yabai -m space $(yabai -m query --spaces | jq -r '[ .[] | select(.\"is-native-fullscreen\" == false) ][-1].index') --label " + label,
  CreateSpace(label = null):
    [
      "yabai -m space --create",
    ] +
    (if label != null then [ LabelLastSpace(label) ] else []),
  DestroySpace(idx):
    [
      "yabai -m space --destroy %(idx)d" % { idx: idx },
    ],
  Padding(top, bottom, left, right, gap):
    [
      self.Config('top_padding', top),
      self.Config('bottom_padding', bottom),
      self.Config('left_padding', left),
      self.Config('right_padding', right),
      self.Config('window_gap', gap),
    ],
  WindowOpacityConfig(state):
    self.Config('window_opacity', state),
  DisableWindowOpacity: self.WindowOpacityConfig('off'),
  WindowOpacity(normal, active):
    [
      self.Comment('Window Opacity'),
      self.WindowOpacityConfig('on'),
      self.Config('normal_window_opacity', normal),
      self.Config('active_window_opacity', active),
    ],
  AddRule(app, args):
    [
      "yabai -m rule --add app=\"%(app)s\" %(rule)s" % {
        app: app,
        rule: (if std.isArray(args) then std.join(' ', args) else args),
      },
    ],
  RuleArg(key, value, quote = false):
    "%(key)s=%(value)s" % {
      key: key,
      value: (if quote then '"' + value + '"' else value)
    },
  AddSpaceRule(app, space, title = null):
    $.AddRule(app,
      [ $.RuleArg("space", space, true) ] +
      (if title != null then [ $.RuleArg("title", title, true) ] else [])
    ),
  ManageRule(state, apps):
    [
      $.AddRule(app, $.RuleArg("manage", state))
      for app in (if std.isArray(apps) then apps else [ apps ])
    ],
  Borders(inactive_color, active_color, width):
    [
      self.Comment('borders'),
      "borders active_color=%(active_color)s inactive_color=%(inactive_color)s width=%(width)s &" % {
        inactive_color: inactive_color,
        active_color: active_color,
        width: width,
      }
    ],
  LoadScripting():
    $.Comment([
      "load scripting",
    ], true) +
    [
      'yabai -m signal --add event=dock_did_restart action="sudo yabai --load-sa"',
      'sudo yabai --load-sa',
    ],
  manifest(config, newline_after):
    std.lines(std.flattenDeepArray(
      $.Comment([
        "** Do not edit **",
        "This file is generated by Jsonnet",
        "# vim: set ft=zsh:"
      ]) +
      $.LoadScripting() +
      config +
      (if newline_after then NEWLINE else []),
    )),
}
