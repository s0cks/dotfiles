local flexoki = import 'lib/flexoki-dark.json';

local cpu = {
  all_entry_color: flexoki['pu'],
  avg_entry_color: flexoki['ma'],
  cpu_core_colors: [
    flexoki['re'],
    flexoki['or'],
    flexoki['ye'],
    flexoki['gr'],
    flexoki['cy'],
    flexoki['bl'],
    flexoki['pu'],
    flexoki['ma'],
  ],
};

local memory = {
  ram_color: flexoki['pu'],
  cache_color: flexoki['ye'],
  swap_color: flexoki['gr'],
  gpu_colors: [
    flexoki['re'],
    flexoki['or'],
    flexoki['ye'],
    flexoki['gr'],
    flexoki['cy'],
    flexoki['bl'],
    flexoki['pu'],
    flexoki['ma'],
  ],
  arc_color: flexoki['bl'],
};

local network = {
  rx_color: "#a6e3a1",
  tx_color: "#f38ba8",
  rx_total_color: "#89dceb",
  tx_total_color: "#a6e3a1",
};

local battery = {
  high_battery_color: "#a6e3a1",
  medium_battery_color: "#f9e2af",
  low_battery_color: "#f38ba8",
};

local Comment(lines) = 
  [
    "# %(line)s" % { line: line },
    for line in (if std.isArray(lines) then lines else [ lines ])
  ];

local Styles(styles) = 
  {
    ["styles.%(name)s" % { name: section.key }]: section.value
    for section in std.objectKeysValues(styles) 
  };

local Bottom(styles) =
  std.manifestToml(
    Styles(styles)
  );

local styles = {
  cpu: cpu,
  memory: memory,
  network: network,
  battery: battery,
  tables: {
    headers: {
      color: "#f5e0dc",
    },
  },
  graphs: {
    graph_color: "#a6adc8",
    legend_text: {
      color: "#a6adc8",
    },
  },
  widgets: {
    border_color: "#585b70",
    selected_border_color: "#f5c2e7",
    widget_title: {color: "#f2cdcd"},
    text: {color: "#cdd6f4"},
    selected_text: {color: "#11111b", bg_color: "#cba6f7"},
    disabled_text: {color: "#1e1e2e"},
  }
};

{
  ["bottom.toml"]:
    std.lines(Comment([
      "** Do not edit **",
      "This file is generated by Jsonnet",
    ])) +
    Bottom(styles),
}
