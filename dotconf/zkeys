#!/usr/bin/env zsh
for key ('^[[A' '^K' ${terminfo[kcuu1]}); bindkey ${key} history-substring-search-up
for key ('^[[B' '^J' ${terminfo[kcud1]}); bindkey ${key} history-substring-search-down
for key ('k'); bindkey -M vicmd ${key} history-substring-search-up
for key ('j'); bindkey -M vicmd ${key} history-substring-search-down
unset key

unsetopt FLOW_CONTROL # disable C-s/C-q in the editor

 # ╭──────╮
 # │ Yazi │
 # ╰──────╯
function yz() {
  if [[ "$1" != "" ]]; then
    if [[ -d "$1" ]]; then
      yazi "$1"
    else
      yazi "$(zoxide query "$1")"
    fi
  else
    yazi
  fi
  return "$?"
}

zle -N yz
bindkey '^\' yz

 # ╭─────────╮
 # │ Lazygit │
 # ╰─────────╯
function __open_lazygit() {
  lazygit
  return "$?"
}
zle -N __open_lazygit 
bindkey '^]' __open_lazygit 

 # ╭────────╮
 # │ Neovim │
 # ╰────────╯
function __open_nvim() {
  nvim
}
zle -N __open_nvim 
bindkey '^[' __open_nvim 

 # ╭──────╮
 # │ Navi │
 # ╰──────╯
function __navi_wiget() {
  local -r input="${LBUFFER}"
  local output="$(navi --print --fzf-overrides '--no-select-1')"

  if [[ "$output" =~ "run: "* ]]; then
    zle push-line
    LBUFFER="${output/run: /}"
    zle accept-line
  elif [[ -n "$output" ]]; then
    LBUFFER="${input}${output}"
  fi
  
  zle redisplay
}

zle -N __navi_wiget 
bindkey '^n' __navi_wiget

 # ╭────────────╮
 # │ Reload zsh │
 # ╰────────────╯
function __reload_zsh() {
  zle -I
  clear
  exec zsh <$TTY
}
zle -N __reload_zsh
bindkey '^[R' __reload_zsh

 # ╭───────────────────────────────────╮
 # │ Open GitHub repository in browser │
 # ╰───────────────────────────────────╯
function __open_in_browser() {
  zle -I
  if [[ -d "$PWD/.git" ]]; then
    gh repo view --web
  else
    open "$PWD"
  fi
}
zle -N __open_in_browser
bindkey '^[O' __open_in_browser

 # ╭────╮
 # │ JQ │
 # ╰────╯
__lbuffer_strip_trailing_pipe() {
  # Strip a trailing pipe and its surrounding whitespace.
  sed -E 's/[[:space:]]*\|[[:space:]]*$//' <<<"$LBUFFER"
}

__get_query() {
  if [ "${JQ_ZSH_PLUGIN_EXPAND_ALIASES:-1}" -eq 1 ]; then
    unset 'functions[_jq-plugin-expand]'
    functions[_jq-plugin-expand]=$(__lbuffer_strip_trailing_pipe)
    (($+functions[_jq-plugin-expand])) && COMMAND=${functions[_jq-plugin-expand]#$'\t'}
    # shellcheck disable=SC2086
    jq-repl -- ${COMMAND}
    return $?
  else
    # shellcheck disable=SC2086
    jq-repl -- $(__lbuffer_strip_trailing_pipe)
    return $?
  fi
}

__jq_explorer() {
  local query
  query="$(__get_query)"
  local ret=$?
  if [ -n "$query" ]; then
    LBUFFER="$(__lbuffer_strip_trailing_pipe) | ${JQ_REPL_JQ:-jq}"
    [[ -z "$JQ_REPL_ARGS" ]] || LBUFFER="${LBUFFER} ${JQ_REPL_ARGS}"
    LBUFFER="${LBUFFER} '$query'"
  fi
  zle reset-prompt
  return $ret
}
zle -N __jq_explorer 
bindkey '\ej' __jq_explorer
